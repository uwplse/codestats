PARSER_BEGIN(QueryParser)

package edu.washington.cse.codestats;

import java.io.*;
import java.util.*;
import edu.washington.cse.codestats.*;

public class QueryParser {
    static List<Query> parse(InputStream in) throws ParseException, TokenMgrError {
         return new QueryParser(in).program();
    }
}

PARSER_END(QueryParser)

SKIP : { " " | "\t" | "\n" | "\r" }

TOKEN :
{
  < EXISTS : "exists">
| < COUNT : "count">
| < STMT : "statement">
| < EXPR : "expression">
| < WITHIN : "within">
| < WHERE : "where">
| < AND : "and" >
| < OR : "or" >
| < IS : "is" >
| < IN : "in" >
| < NOT : "not" >
| < ANY : "[?]" >
| < ALL : "[*]" >
| < THIS : "[0]" >
| < IDENT : (["A"-"Z", "a"-"z", "_"])+ >
| < STRING : "\"" (["A"-"Z", "a"-"z", "0"-"9", "_", "."])* "\"" >
| < OPERATOR : ("!=" | "==" | "<" | "<=" | ">=" | ">") >
}

List<Query> program() :
{
    List<Query> blocks = new ArrayList();
    Query block;
}
{
    ((block = block()) { blocks.add(block); })* <EOF> { return blocks; }
}

Query block() :
{
    Token name = null;
    Token deriving = null;
    Metric metric = null;
    QueryTarget target = null;
    PredicateMirror pred = null;
}
{
    (name = <IDENT>) ":" (<EXISTS> { metric = Metric.EXISTS; } | <COUNT> { metric = Metric.SUM; })
    (<EXPR> { target = QueryTarget.EXPRESSION; } | <STMT> { target = QueryTarget.STATEMENT; })
    <IDENT> [<WITHIN> (deriving = <IDENT>)] <WHERE> "{" (pred = predicate()) "}"
    { return new Query(name.image, deriving == null ? null : deriving.image, metric, target, pred); }
}

PredicateMirror predicate() :
{
    PredicateMirror pred;
}
{
    (pred = conjunction())
    { return pred; }
}

PredicateMirror conjunction() :
{
    List<PredicateMirror> children = new ArrayList<PredicateMirror>();
    PredicateMirror child;
}
{
    (child = disjunction()) { children.add(child); }
    (<AND> (child = disjunction()) { children.add(child); } )*
    { return new PredicateMirror(PredicateMirror.PredicateType.AND, children); }
}

PredicateMirror disjunction() :
{
    List<PredicateMirror> children = new ArrayList<PredicateMirror>();
    PredicateMirror child;
}
{
    (child = atom_or_conjunction()) { children.add(child); }
    (<OR> (child = atom_or_conjunction()) { children.add(child); } )*
    { return new PredicateMirror(PredicateMirror.PredicateType.OR, children); }
}

PredicateMirror atom_or_conjunction() :
{
    PredicateMirror pred;
}
{
    ("(" (pred = conjunction()) ")" | (pred = atom()))
    { return pred; }
}

PredicateMirror atom() :
{
    List<String> attribute = new ArrayList<String>();
    Token component = null;
    Token target = null;
    Token operator = null;
    boolean is = true;
}
{
    <IDENT> ( "." (component = <IDENT>) { attribute.add(component.image); } | <ANY> { attribute.add("?"); } | <ALL> { attribute.add("*"); } | <THIS> { attribute.add("0"); } )*
    ( (operator = <OPERATOR>) | <IS> [ <NOT> { is = false; } ]) (target = <STRING>)

    {
        PredicateAtom atom;
        if (operator != null) {
            atom = new PredicateAtom(attribute, operator.image, target.image.substring(1, target.image.length() - 1));
        } else {
            atom = new PredicateAtom(attribute, is, target.image(1, target.image.length() - 1));
        }
        return new PredicateMirror(atom);
    }
}